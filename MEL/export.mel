global proc exocortexCloseDialog(string $dialog)
{
  deleteUI -window $dialog;
}

global proc exocortexAlembicExport(string $filename,         string $exported_objects[], int $exportInframe,
                                   int $exportOutframe,      int $exportStepframe,       int $exportSubstepframe,
                                   int $exportTopology,      int $exportUVs,             int $exportFaceSets,
                                   int $exportDynamicTopo,   int $exportGlobalSpace,     int $exportWithoutHierarchy)
{
  // construct the job string
  string $job = ("in="+$exportInframe+";out="+$exportOutframe+";step="+$exportStepframe+";substep="+$exportSubstepframe+";filename="+$filename+";objects=");
  for($i=0;$i<size($exported_objects);$i++)
  {
    if($i>0)
      $job += ",";
    $job += $exported_objects[$i];
  }
  print $exportTopology;
  if($exportTopology == 1)    
  {
    $job += ";purepointcache=1;dynamictopology=0;normals=0;uvs=0;facesets=0";
  }
  else if($exportTopology == 2)    
  {
    $job += ";purepointcache=0;normals=0;dynamictopology=0";
    $job += ";uvs="+$exportUVs;
    $job += ";facesets="+$exportFaceSets;
  }
  else if($exportTopology == 3)    
  {
    $job += ";purepointcache=0;normals=1";
    $job += ";uvs="+$exportUVs;
    $job += ";facesets="+$exportFaceSets;
    $job += ";dynamictopology="+$exportDynamicTopo;
  }
  $job += ";globalspace="+$exportGlobalSpace;
  $job += ";withouthierarchy="+$exportWithoutHierarchy;
  print $job;
  ExocortexAlembic_export -j $job;
}

global proc exocortexAlembicExportGUI(string $filename, string $dialog)
{
  // parse all options
  string $exportInframe = `textField -q -text inframe`;
  string $exportOutframe = `textField -q -text outframe`;
  string $exportStepframe = `textField -q -text stepframe`;
  string $exportSubstepframe = `textField -q -text substepframe`;
  int $exportTopology = `optionMenu -q -select topology`;
  int $exportUVs = `checkBox -q -value uvs`;
  int $exportFaceSets = `checkBox -q -value facesets`;
  int $exportDynamicTopo = `checkBox -q -value dynamictopology`;
  int $exportGlobalSpace = `checkBox -q -value globalspace`;
  int $exportWithoutHierarchy = `checkBox -q -value withouthierarchy`;
  exocortexCloseDialog($dialog);

  $minTime = `playbackOptions -q -min`;
  $maxTime = `playbackOptions -q -max`;

  // check selection
  string $selection[] = `ls -sl`;
  if(size($selection) == 0)
  {
    error("Nothing selected!");
    return;
  }
  
  // check filename
  if($filename == "")
  {
    $files = `fileDialog2 -ds 2 -cap "Choose the Alembic File To Export" -ff "Alembic Files (*.abc)" -fm 0`;
    print $files;
    if(size($files) == 0)
    {
      print("Export aborted by user.");
      return;
    }
    $filename = $files[0];
  }
  $filename = `ExocortexAlembic_resolvePath -f $filename`;
  exocortexAlembicExport($filename, $selection, $exportInframe, $exportOutframe, $exportStepframe, $exportSubstepframe, $exportTopology, $exportUVs, $exportFaceSets, $exportDynamicTopo, $exportGlobalSpace, $exportWithoutHierarchy);
}

global proc exocortexCloseDialog(string $dialog)
{
  deleteUI -window $dialog;
}

global proc exocortexChooseFile(string $dialog)
{
  exocortexAlembicExportGUI("",$dialog);
}

global proc exocortexSetupDialog(string $uiDirectory)
{
  global string $dialog;
  if (`window -exists $dialog`)
    deleteUI -window $dialog;

  $dialog = `loadUI -uiFile ($uiDirectory+"/ExocortexAlembicExport.ui")`;
  windowPref -topEdge 300 -leftEdge 300 $dialog;
  textField -edit -text `playbackOptions -q -min` inframe;
  textField -edit -text `playbackOptions -q -max` outframe;
  showWindow $dialog;
}


proc extractInfos(string $infos[], string $identifiers[], string $types[], string $names[], string $nodes[], string $datas[], int $nbSamples[], int $constants[])
{
  for($i=0; $i<size($infos); $i=$i+1)
  {
    // tokenize the basic arguments
    string $tokens[];
    tokenize $infos[$i] "|" $tokens;
    if(size($tokens) < 4)
    {
      print ("Skipping invalid info "+$infos[$i]+".");
      continue;
    }

    // extract the information
    string $identifier = $tokens[0];
    string $type = $tokens[1];
    string $name = $tokens[2];
    string $node = `ExocortexAlembic_getNodeFromIdentifier -i $identifier`;

    // push the identifier to our map      
    $identifiers[size($identifiers)] = $identifier;
    $types[size($types)] = $type;
    $names[size($names)] = $tokens[2];
    $nodes[size($nodes)] = $node;
    $nbSamples[size($nbSamples)] = $tokens[3];
    $constants[size($constants)] = $tokens[6];
    
    // parse the forth token
    string $data = "";
    if(size($tokens)>7)
      $data = $tokens[7];
    $datas[size($datas)] = $data;
  }
}

proc string[] createTimeControl(string $filename)
{
  string $ret[];

  string $timeControl = `createNode "ExocortexAlembicTimeControl"`;
  connectAttr "time1.outTime" ($timeControl+".inTime");
  string $fileNode = `createNode "ExocortexAlembicFile"`;
  setAttr -type "string" ($fileNode+".fileName ") $filename;

  $ret[0] = $timeControl;
  $ret[1] = $fileNode;
  return $ret;
}

proc attachTimeAndFile(string $node, string $eaTimeFile[], int $constant)
{
  string $connAttr = `connectionInfo -sfd ($node+".inTime")`;
  if ($connAttr == "" && $constant == 0)
    connectAttr ($eaTimeFile[0]+".outTime") ($node+".inTime");

  $connAttr = `connectionInfo -sfd ($node+".fileName")`;
  if ($connAttr != "")
    disconnectAttr $connAttr ($node+".fileName");
  connectAttr ($eaTimeFile[1]+".outFileName") ($node+".fileName");
}

proc attachXform(string $name, string $identifier, string $eaTimeFile[], int $constant)
{
  ExocortexAlembic_profileBegin -f MEL_exocortexAlembic_attachXform;
  string $cons[] = `listConnections ($name+".translate")`;
  if (size($cons) > 0)
  {
    if (`objectType $cons[0]` == "ExocortexAlembicXform")
    {
      string $fileN[] = `listConnections ($cons[0]+".fileName")`;
      if (size($fileN) > 0)
        disconnectAttr ($fileN[0]+".outFileName") ($cons[0]+".fileName");
      connectAttr ($eaTimeFile[1]+".outFileName") ($cons[0]+".fileName");
    }
    else
      print("Cannot attach Xform to " + $name + ", it's attach to a node that is not an \"ExocortexAlembicXform\"");
    return;
  }

  string $newXform = `createNode -name ("Xform" + $name) "ExocortexAlembicXform"`;
  setAttr -type "string" ($newXform + ".identifier") $identifier;

  connectAttr ($newXform+".translate") ($name+".translate");
  connectAttr ($newXform+".rotate") ($name+".rotate");
  connectAttr ($newXform+".scale") ($name+".scale");

  attachTimeAndFile($newXform, $eaTimeFile, $constant);
  ExocortexAlembic_profileEnd -f MEL_exocortexAlembic_attachXform;
}

proc attachPolyMeshDeformer(string $name, string $identifier, string $eaTimeFile[], int $importNormals, int $importUvs, int $importFaceSets, int $constant)
{
  ExocortexAlembic_profileBegin -f MEL_exocortexAlembic_attachPolyMeshDeformer;
  // get first the poly object (polyCube, polySphere, ...)
  string $inMeshConnect = `connectionInfo -sfd ($name + ".inMesh")`;
  string $polyObj = `plugNode $inMeshConnect`;

  // if it's already attach to a deform, simply change its file reference
  if (`objectType $polyObj` == "ExocortexAlembicPolyMeshDeform")
  {
    attachTimeAndFile($polyObj, $eaTimeFile, $constant);
    return;
  }

  // create deformer, and attach time and file
  string $newDform[] = `deformer -type "ExocortexAlembicPolyMeshDeform" -name ("dForm_" + $name) $name`;
  setAttr -type "string" ($newDform[0] + ".identifier") $identifier;
  attachTimeAndFile($newDform[0], $eaTimeFile, $constant);

  // get polyObj new "output" attribute connection
  string $originalMesh = $name;
  if ($polyObj != "" && (`objectType $polyObj` != "ExocortexAlembicPolyMesh"))
  {
    string $inMeshConnect2[] = `connectionInfo -dfs ($polyObj + ".output")`;
    $originalMesh = `plugNode $inMeshConnect2[0]`;

    // delete the original poly object, create the alembic poly mesh, and attach time and file, and connected it to the original object!
    delete $polyObj;
    $polyObj = `createNode -name ("alembicPolyMesh_" + $name) "ExocortexAlembicPolyMesh"`;
    attachTimeAndFile($polyObj, $eaTimeFile, $constant);
    connectAttr ($polyObj + ".outMesh") ($originalMesh + ".inMesh");
    setAttr ($polyObj+".normals") $importNormals;
    setAttr ($polyObj+".uvs") $importUvs;
  }

  if ($importFaceSets)
  {
    string $fname = `getAttr ($eaTimeFile[1]+".outFileName")`;
    ExocortexAlembic_createFaceSets -f $fname -i $identifier -o $name;
  }
  ExocortexAlembic_profileEnd -f MEL_exocortexAlembic_attachPolyMeshDeformer;
}

proc attachSubDDeformer(string $name, string $identifier, string $eaTimeFile[], int $importUvs, int $importFaceSets, int $constant)
{
  ExocortexAlembic_profileBegin -f MEL_exocortexAlembic_attachSubDDeformer;
  // get first the potential deform object!
  string $inCreateConnect = `connectionInfo -sfd ($name + ".create")`;
  if ($inCreateConnect != "")
  {
    string $polyObj = `plugNode $inCreateConnect`;

    // if it's already attach to a deform, simply change its file reference
    if (`objectType $polyObj` == "ExocortexAlembicSubDDeform")
    {
      attachTimeAndFile($polyObj, $eaTimeFile, $constant);
    }
    else
      print("Cannot attach to " + $name + ", it's already attach to a deform node that is not an \"ExocortexAlembicSubDDeform\"");
    return;
  }

  // create deformer
  string $newDform[] = `deformer -type "ExocortexAlembicSubDDeform" -name ("dForm_" + $name) $name`;
  setAttr -type "string" ($newDform[0] + ".identifier") $identifier;
  attachTimeAndFile($newDform[0], $eaTimeFile, $constant);

  // create alembic subd and connect it to the original object ($name+"Orig" and $name)
  $subdObj = `createNode -name ("alembicSubD_" + $name) "ExocortexAlembicSubD"`;
  attachTimeAndFile($subdObj, $eaTimeFile, $constant);
  connectAttr ($subdObj + ".dispResolution") ($name + ".dispResolution");
  connectAttr ($subdObj + ".outSubdiv") ($name + "Orig.create");
  setAttr ($subdObj+".uvs") $importUvs;
  if ($importFaceSets)
  {
    string $fname = `getAttr ($eaTimeFile[1]+".outFileName")`;
    ExocortexAlembic_createFaceSets -f $fname -i $identifier -o $name;
  }
  ExocortexAlembic_profileEnd -f MEL_exocortexAlembic_attachSubDDeformer;
}

global proc attachToExisting(string $filename, int $importNormals, int $importUvs, int $importFaceSets, int $overTransforms, int $overDeforms)
{
  ExocortexAlembic_profileBegin -f MEL_exocortexAlembic_attachToExisting;
  string $identifiers[];
  string $types[];
  string $names[];
  string $nodes[];
  string $datas[];
  int $nbSamples[];
  int $constants[];

  if (!$overTransforms && !$overDeforms)
    return;

  extractInfos(`ExocortexAlembic_getInfo -f $filename`, $identifiers, $types, $names, $nodes, $datas, $nbSamples, $constants);

  string $eaTimeFile[];   // will old the TimeControl name and the File Node name

  int $nbIds = size($identifiers);
  int $ii = 0;
  for (; $ii < $nbIds; $ii = $ii + 1)
  {
    string $name = $names[$ii];
    if (`objExists $name` == 0)
    {
      print("Could not find " + $name);
      continue;
    }

    string $type = $types[$ii];
    int $constant = $constants[$ii];
    if($overTransforms)
    {
      if ($type == "Xform")
      {
        if (size($eaTimeFile) == 0)
          $eaTimeFile = createTimeControl($filename);
        attachXform($name, $identifiers[$ii], $eaTimeFile, $constant);
      }
    }

    if ($overDeforms)
    {
      if($type == "PolyMesh")
      {
        if (size($eaTimeFile) == 0)
          $eaTimeFile = createTimeControl($filename);
        attachPolyMeshDeformer($name, $identifiers[$ii], $eaTimeFile, $importNormals, $importUvs, $importFaceSets, $constant);
      }
      else if($type == "SubD")
      {
        if (size($eaTimeFile) == 0)
          $eaTimeFile = createTimeControl($filename);
        attachSubDDeformer($name, $identifiers[$ii], $eaTimeFile, $importUvs, $importFaceSets, $constant);
      }
      else if ($type != "Xform")
        print("Type \"" + $type + "\" not supported yet to attach!");
    }
  }
  ExocortexAlembic_profileEnd -f MEL_exocortexAlembic_attachToExisting;
}

global proc exocortexAlembicAttachGUI(string $filename, string $dialog)
{
  // parse all options
  int $importNormals = `checkBox -q -value normals`;
  int $importUvs = `checkBox -q -value uvs`;
  int $importFaceSets = `checkBox -q -value facesets`;
  int $overTransforms = `checkBox -q -value otransform`;
  int $overDeforms = `checkBox -q -value odeform`;
  exocortexCloseDialog($dialog);

  // check if the filename is valid
  if($filename == "")
  {
    $files = `fileDialog2 -ds 2 -cap "Choose the Alembic File To Import" -ff "Alembic Files (*.abc)" -fm 1`;
    print $files;
    if(size($files) == 0)
    {
      print("Import aborted by user.");
      return;
    }
    $filename = $files[0];
  }
  
  // check if the file exists
  string $resolvedPath = `ExocortexAlembic_resolvePath -f $filename`;
  if(!`filetest -r $resolvedPath`)
  {
    error ("File "+$filename+" does not exist!");
    return;
  }
  attachToExisting($filename, $importNormals, $importUvs, $importFaceSets, $overTransforms, $overDeforms);
}

global proc exocortexCloseDialog(string $dialog)
{
  deleteUI -window $dialog;
}

global proc exocortexChooseFile(string $dialog)
{
  exocortexAlembicAttachGUI("",$dialog);
}

global proc exocortexSetupDialog(string $uiDirectory)
{
  global string $dialog;
  if (`window -exists $dialog`)
    deleteUI -window $dialog;

  $dialog = `loadUI -uiFile ($uiDirectory+"/ExocortexAlembicAttach.ui")`;
  windowPref -topEdge 300 -leftEdge 300 $dialog;
  showWindow $dialog;
}

